#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
# Take an XYZ format molecular file
# (https://en.wikipedia.org/wiki/XYZ_file_format) and convert
# coordinates to orthogonal ones (assuming that they were fractional
# in the original file). Unit cell constants can be given as command
# line option parameters.
#**

use strict;
use warnings;

use COD::Fractional qw(symop_ortho_from_fract);

use COD::SOptions qw( getOptions get_value );
use COD::SUsage qw( usage options );

my $cell;

my $float_format = "%15.6f";

@ARGV = getOptions(
    '-c,--cell'         => \$cell,
    '-f,--float-format' => \$float_format,
    '--options'         => sub { options; exit },
    '--help,--usage'    => sub { usage; exit },
);

my @cell = ();

if( $cell ) {
    $cell =~ s/,/ /g;
    @cell = split( ' ', $cell );
}

my $o4f = $cell ?
    symop_ortho_from_fract( @cell ) : [[1,0,0],[0,1,0],[0,0,1]];

while(<>) {
    my $N = $_;
    my $comment = <>;

    chomp($comment);

    my @atoms;
    for my $i (0..$N-1) {
        push( @atoms, [split(" ", <>)] );
    }

    for my $a (@atoms) {
        my ($x, $y, $z);
        $x = $o4f->[0][0] * $a->[1] + $o4f->[0][1] * $a->[2] + $o4f->[0][2] * $a->[3];
        $y = $o4f->[1][0] * $a->[1] + $o4f->[1][1] * $a->[2] + $o4f->[1][2] * $a->[3];
        $z = $o4f->[2][0] * $a->[1] + $o4f->[2][1] * $a->[2] + $o4f->[2][2] * $a->[3];
        ($a->[1],$a->[2],$a->[3]) = ($x,$y,$z);
    }

    print_atoms( \@atoms, $comment, \@cell );
}

sub print_atoms
{
    my ($atoms, $comment, $cell) = @_;

    print int(@$atoms), "\n";
    print $comment;
    if( $cell ) {
        local $, = " ";
        print " CELL:", @{cell};
    }
    print "\n";

    for my $atom (@$atoms) {
        printf "%-8s $float_format $float_format $float_format\n", @$atom;
    }
}
